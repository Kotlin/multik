/*
 * Copyright 2020-2021 JetBrains s.r.o. Use of this source code is governed by the Apache 2.0 license.
 */

import org.gradle.internal.os.OperatingSystem

ext {
    openblasBranch = "v$openblas_version"
    openblasRoot = openblas_version
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'de.undercouch:gradle-download-task:4.1.2'
    }
}

apply plugin: 'de.undercouch.download'

static boolean isWindows() {
    return System.getProperty('os.name').toLowerCase().contains('windows')
}

def systemOpenBlasParam() {
    switch (OperatingSystem.current()) {
        case OperatingSystem.WINDOWS: {
            project.ext["opblParam"] = [cc: "gcc -m64",
                                        fc: "gfortran -m64",
                                        fextralib: "-lgfortran -lquadmath",
                                        noAVX512: "1"]
            break
        }
        case OperatingSystem.LINUX: {
            project.ext["opblParam"] = [cc: "gcc -m64",
                                        fc: "gfortran -m64",
                                        fextralib: "-lgfortran",
                                        noAVX512: "0"]
            break
        }
        case OperatingSystem.MAC_OS: {
            project.ext["opblParam"] = [cc: "/usr/local/bin/gcc-11",
                                        fc: "/usr/local/bin/gfortran-11",
                                        fextralib: "-lgfortran",
                                        noAVX512: "1"]
            break
        }
        default: break
    }
}

task downloadOpenBlas(type: Download) {
    if (!isWindows()) {
        src "https://github.com/xianyi/OpenBLAS/archive/${openblasBranch}.zip"
    } else {
        src "https://github.com/xianyi/OpenBLAS/releases/download/${openblasBranch}/OpenBLAS-${openblasRoot}-x64.zip"
    }
    dest new File(buildDir, "openblas.${openblasBranch}.zip")
    overwrite false
}

task unzipOpenBlas(dependsOn: downloadOpenBlas, type: Copy) {
    from zipTree(downloadOpenBlas.dest)
    if (!isWindows()) {
        into buildDir
    } else {
        outputs.dir new File(buildDir as File, "openblas")
        into "$buildDir/openblas"
    }
}

def nullOutputStream = new OutputStream() {
    @Override
    void write(int b) {}
}

task installOpenBlas(dependsOn: unzipOpenBlas) {
    if (!isWindows()) {
        def wrkDirOpenBlas = "$buildDir/OpenBLAS-${openblasRoot}"
        systemOpenBlasParam()
        outputs.dir new File(buildDir, 'openblas')
        doLast {
            exec {
                workingDir wrkDirOpenBlas
                standardOutput = nullOutputStream
                commandLine("make", "clean")
                commandLine(
                        "make",
                        "CC=${opblParam.cc}",
                        "FC=${opblParam.fc}",
                        "HOSTCC=gcc",
                        "BINARY=64",
                        "F_COMPILER=GFORTRAN",
                        "FEXTRALIB=${opblParam.fextralib}",
                        "USE_OPENMP=0",
                        "NO_AVX512=${opblParam.noAVX512}",
                        "DYNAMIC_ARCH=1",
                        "NUM_THREADS=64"
                )
            }
            exec {
                workingDir wrkDirOpenBlas
                standardOutput = nullOutputStream
                commandLine("make", "install", "PREFIX=$buildDir/openblas")
            }
        }
    }
}